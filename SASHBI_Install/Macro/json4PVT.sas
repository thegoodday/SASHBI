%macro json4PVT(tableName=,class=, var=, rows=, cols=);
	%let old = %sysfunc(stpsrv_header(Content-type, text/json%str(;) encoding=utf-8));
	%let old = %sysfunc(stpsrv_header(Pragma, nocache));
	%put NOTE: Rows : &rows ;
	%put NOTE: Cols : &cols ;
	proc summary data=&tableName nway missing;
		class &class ;
		var &var ;
		output out=work.summ sum=;
	proc sort data=work.summ;
		by &class;
	proc transpose data=work.summ out=work.target prefix=_ANALYSIS;
		by &class ;
		var &var ;
	run;
	
	data _null_;
		if libref('save') then do;
			rc=stpsrv_session('create');
			rc=stpsrvset('session timeout',60*60*24);
		end;
	run;	
	data _null_;
		length cmd $1024. ;
		file _webout;
		save_path=pathname('save');
		save_path=TRANSLATE(save_path,"/","\");
		call symput("save_path",trim(save_path));
	run;  	
	data save.WEBDATA;
		set &tableName;
	run;
	%let classNum=%eval(%length(&class)-%length(%sysfunc(compress(&class,%str( ))))+1);
	%let varNum=%eval(%length(&var)-%length(%sysfunc(compress(&var,%str( ))))+1);
	%let rowNum=%eval(%length(&rows)-%length(%sysfunc(compress(&rows,%str( ))))+1);
	%let colNum=%eval(%length(&cols)-%length(%sysfunc(compress(&cols,%str( ))))+1);

	%put NOTE: json4PVT classNum : &classNum;
	%put NOTE: json4PVT varNum	    : &varNum;
	proc json out=_webout;
		write open array;
			write open object;
				write value "TableInfo";
					write open object;
						write values rows ;
							write open array;
							%do ii=1 %to &rowNum;
								%let rowVar=%scan(&rows,&ii,%str( ));
								write values "&rowVar" ;
							%end;
							write close;
						write values cols ;
							write open array;
							%do ii=1 %to &colNum;
								%let colVar=%scan(&cols,&ii,%str( ));
								write values "&colVar" ;
							%end;
								write values "_LABEL_" ;
							write close;
						write values vals ;
							write open array;
								write values "_ANALYSIS1" ;
							write close;
					write close;
				write value "SessionInfo";
				write open array;
					write open object;
						write values nstp_sessionid "&_SESSIONID";
						write values save_path "&save_path";
					write close;
				write close;
				write value "SASResult";
				write open array;
						export work.target (keep=
						%put NOTE: classNum = &classNum;
						%put NOTE: class = &class;
						%do ii=1 %to &classNum;
							%let varName=%scan(&class,&ii,%str( ));
							&varName
						%end;
						_NAME_ _LABEL_ _ANALYSIS1
						) / nosastags ;
				write close;
			write close;
		write close;
	run;	
%mend json4PVT;
